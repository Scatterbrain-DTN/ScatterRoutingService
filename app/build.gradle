plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
    id 'groovyx.android'
    id("org.jetbrains.dokka") version "1.5.0"
    id 'jacoco'
}

def protobuf_version = "3.18.0"

android {
    buildToolsVersion "30.0.3"
    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 32
        compileSdkVersion 31
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental": "true",
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    packagingOptions {
        pickFirst 'META-INF/com.google.dagger_dagger.version'
    }

    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    sourceSets{
        main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:$protobuf_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    def lifecycle_version = "2.4.1"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
    implementation "androidx.room:room-runtime:2.4.2"
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 3.x.x version)
    implementation("androidx.security:security-crypto:1.1.0-alpha03")

    //reflect for logging
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.6.20'

    implementation project(':scatterbrainSDK')
    androidTestImplementation project(':scatterbrainSDK')
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    implementation "com.polidea.rxandroidble2:rxandroidble:1.13.1"
    kapt "androidx.room:room-compiler:2.4.2" // For Kotlin use kapt instead of annotationProcessor
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    // The following Espresso dependency can be either "implementation"
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APK's compile classpath or the test APK
    // classpath.
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation "androidx.room:room-testing:2.4.2"

    //lazysodium cryptographic library
    implementation "com.goterl:lazysodium-android:5.0.2@aar"
    implementation 'net.java.dev.jna:jna:5.8.0@aar'

    implementation "com.google.dagger:dagger:2.40.1"
    testImplementation "com.google.dagger:dagger:2.40.1"

    kapt "com.google.dagger:dagger-compiler:2.40.1"
    kaptTest "com.google.dagger:dagger-compiler:2.40.1"


    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation "androidx.room:room-rxjava2:2.4.2"

    implementation "com.github.davidmoten:rxjava2-extras:0.1.38"
    testImplementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    testImplementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    testImplementation "androidx.room:room-rxjava2:2.4.2"

    testImplementation "com.github.davidmoten:rxjava2-extras:0.1.38"

    implementation "androidx.core:core-ktx:1.7.0"
    androidTestImplementation 'androidx.test:core:1.4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.firebase:firebase-crashlytics:18.2.10'
    androidTestImplementation 'com.google.firebase:firebase-crashlytics:18.2.10'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    // Required -- JUnit 4 framework
    testImplementation "junit:junit:4.13.2"
    // Optional -- Robolectric environment
    testImplementation "androidx.test:core:1.4.0"
    testImplementation 'org.robolectric:robolectric:4.8'
    // Optional -- Mockito framework
    testImplementation "org.mockito:mockito-core:4.0.0"
    // Optional -- mockito-kotlin
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    // Optional -- Mockk framework
    testImplementation "io.mockk:mockk:1.9.1"
}
repositories {
    mavenCentral()
}


task jacocoTestReport(type: JacocoReport, dependsOn: ['createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}
